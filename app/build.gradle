plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.akaita.android.easylauncher'
    id 'io.fabric'
}

def keystorePropertiesFile = rootProject.file("secret.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion rootProject.ext.buildConfig.compileSdkVersion

    defaultConfig {
        versionCode 1
        versionName '1.0.0'

        applicationId "at.florianschuster.playables"
        setProperty("archivesBaseName", "Playables-v$versionName-b$versionCode")

        targetSdkVersion rootProject.ext.buildConfig.targetSdkVersion
        minSdkVersion rootProject.ext.buildConfig.minSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue "string", "app_name", "Playables"
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix '-DEBUG'
            minifyEnabled false
        }
        release {
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = false
        }
        abi {
            enableSplit = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError true
    }

    packagingOptions {
        pickFirst 'META-INF/kotlinx-io.kotlin_module'
        pickFirst 'META-INF/atomicfu.kotlin_module'
        pickFirst 'META-INF/kotlinx-coroutines-io.kotlin_module'
        pickFirst 'META-INF/kotlinx-coroutines-core.kotlin_module'
        pickFirst 'META-INF/kotlinx-serialization-runtime.kotlin_module'
    }
}

androidExtensions {
    experimental = true
}

apply from: rootProject.file("gradle/test-dependencies.gradle")
apply from: rootProject.file("gradle/test-dependencies-android.gradle")

dependencies {
    implementation(project(path: ':core-android'))

    implementation Libs.appcompat
    implementation Libs.activity_ktx
    implementation Libs.fragment_ktx
    implementation Libs.constraintlayout
    implementation Libs.material
    implementation Libs.browser
    implementation Libs.collection_ktx

    implementation Libs.util_ui

    implementation Libs.lifecycle_extensions
    implementation Libs.lifecycle_viewmodel_ktx
    implementation Libs.lifecycle_livedata_ktx
    implementation Libs.lifecycle_runtime_ktx
    kapt Libs.lifecycle_compiler

    implementation Libs.flowbinding_android
    implementation Libs.flowbinding_core
    implementation Libs.flowbinding_appcompat
    implementation Libs.flowbinding_recyclerview
    implementation Libs.flowbinding_material
    implementation Libs.exoplayer

    implementation Libs.koin_androidx_viewmodel

    implementation Libs.coil
    implementation Libs.shimmer
    implementation Libs.insetter_ktx

    implementation Libs.timber
    debugImplementation Libs.leakcanary_android
    releaseImplementation Libs.leakcanary_android_no_op
}

apply plugin: 'com.google.gms.google-services'

easylauncher {
    iconNames "@mipmap/ic_launcher_foreground"
    foregroundIconNames "@mipmap/ic_launcher_foreground"

    defaultFlavorNaming = true

    buildTypes {
        debug {
            filters = customColorRibbonFilter("debug", "#47BAF2", "#ffffff")
        }
        release {}
    }
}
